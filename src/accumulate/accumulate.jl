# Available accumulation algorithms
abstract type AccumulateAlgorithm end
struct DecoupledLookback <: AccumulateAlgorithm end
struct ScanPrefixes <: AccumulateAlgorithm end


# Helpers
# Check the given dst is compatible with src and init
function _accumulate_check_types(dst, src, init)
    eltype(dst) === eltype(src) && return
    eltype(dst) === typeof(init) && return
    eltype(dst) === promote_type(eltype(src), typeof(init)) && return

    throw(ArgumentError(
        """
        destination array type `$(eltype(dst))` (temp) is incompatible with source array type
        `$(eltype(src))` and initial value type `$(typeof(init))`; eltype(dst) must be either
        like eltype(src) or typeof(init) or promote_type(eltype(src), typeof(init)).
        """
    ))
end


# Implementations, then interfaces
include("accumulate_1d.jl")
include("accumulate_nd.jl")
include("accumulate_cpu.jl")


"""
    accumulate!(
        op, v::AbstractArray, backend::Backend=get_backend(v);
        init,
        neutral=GPUArrays.neutral_element(op, eltype(v)),
        dims::Union{Nothing, Int}=nothing,
        inclusive::Bool=true,

        # Algorithm choice
        alg::AccumulateAlgorithm=DecoupledLookback(),

        # GPU settings
        block_size::Int=256,
        temp::Union{Nothing, AbstractArray}=nothing,
        temp_flags::Union{Nothing, AbstractArray}=nothing,
    )

    accumulate!(
        op, dst::AbstractArray, src::AbstractArray, backend::Backend=get_backend(v);
        init,
        neutral=GPUArrays.neutral_element(op, eltype(dst)),
        dims::Union{Nothing, Int}=nothing,
        inclusive::Bool=true,

        # Algorithm choice
        alg::AccumulateAlgorithm=DecoupledLookback(),

        # GPU settings
        block_size::Int=256,
        temp::Union{Nothing, AbstractArray}=nothing,
        temp_flags::Union{Nothing, AbstractArray}=nothing,
    )

Compute accumulated running totals along a sequence by applying a binary operator to all elements
up to the current one; often used in GPU programming as a first step in finding / extracting
subsets of data.

**Other names**: prefix sum, `thrust::scan`, cumulative sum; inclusive (or exclusive) if the first
element is included in the accumulation (or not).

For compatibility with the `Base.accumulate!` function, we provide the two-array interface too, but
we do not need the constraint of `dst` and `src` being different; to minimise memory use, we
recommend using the single-array interface (the first one above).

## CPU 
The CPU implementation is currently single-threaded; we are waiting on a multithreaded
implementation in OhMyThreads.jl ([issue](https://github.com/JuliaFolds2/OhMyThreads.jl/issues/129)).

## GPU 
For the 1D case (`dims=nothing`), the `alg` can be one of the following:
- `DecoupledLookback()`: the default algorithm, using opportunistic lookback to reuse earlier
  blocks' results; requires device-level memory consistency guarantees, which Apple Metal does not
  provide.
- `ScanPrefixes()`: a simpler algorithm that scans the prefixes of each block, with no lookback; it
  has similar performance as `DecoupledLookback()` for large block sizes, and small to medium arrays,
  but poorer scaling for many blocks; there is no performance degradation below `block_size^2`
  elements.

A different, unique algorithm is used for the multi-dimensional case (`dims` is an integer).

The `block_size` should be a power of 2 and greater than 0.

The temporaries are only used for the 1D case (`dims=nothing`): `temp` stores per-block aggregates;
`temp_flags` is only used for the `DecoupledLookback()` algorithm for flagging if blocks are ready;
they should both have at least `(length(v) + 2 * block_size - 1) รท (2 * block_size)` elements; also,
`eltype(v) === eltype(temp)` is required; the elements in `temp_flags` can be any integers, but
`Int8` is used by default to reduce memory usage.

# Platform-Specific Notes
On Metal, the `alg=ScanPrefixes()` algorithm is used by default, as Apple Metal GPUs do not have
strong enough memory consistency guarantees for the `DecoupledLookback()` algorithm - which
produces incorrect results about 0.38% of the time (the beauty of parallel algorithms, ey). Also,
`block_size=1024` is used here by default to reduce the number of coupled lookbacks.

# Examples
Example computing an inclusive prefix sum (the typical GPU "scan"):
```julia
import AcceleratedKernels as AK
using oneAPI

v = oneAPI.ones(Int32, 100_000)
AK.accumulate!(+, v, init=0)

# Use a different algorithm
AK.accumulate!(+, v, alg=AK.ScanPrefixes())
```
"""
function accumulate!(
    op, v::AbstractArray, backend::Backend=get_backend(v);
    init,
    neutral=GPUArrays.neutral_element(op, eltype(v)),
    dims::Union{Nothing, Int}=nothing,
    inclusive::Bool=true,

    # Algorithm choice
    alg::AccumulateAlgorithm=DecoupledLookback(),

    # GPU settings
    block_size::Int=256,
    temp::Union{Nothing, AbstractArray}=nothing,
    temp_flags::Union{Nothing, AbstractArray}=nothing,
)
    _accumulate_impl!(
        op, v, backend,
        init=init, neutral=neutral, dims=dims, inclusive=inclusive,
        alg=alg,
        block_size=block_size, temp=temp, temp_flags=temp_flags,
    )
end


function accumulate!(
    op, dst::AbstractArray, src::AbstractArray, backend::Backend=get_backend(v);
    init,
    neutral=GPUArrays.neutral_element(op, eltype(dst)),
    dims::Union{Nothing, Int}=nothing,
    inclusive::Bool=true,

    # Algorithm choice
    alg::AccumulateAlgorithm=DecoupledLookback(),

    # GPU settings
    block_size::Int=256,
    temp::Union{Nothing, AbstractArray}=nothing,
    temp_flags::Union{Nothing, AbstractArray}=nothing,
)
    copyto!(dst, src)
    _accumulate_impl!(
        op, dst, backend,
        init=init, neutral=neutral, dims=dims, inclusive=inclusive,
        alg=alg,
        block_size=block_size, temp=temp, temp_flags=temp_flags,
    )
end


function _accumulate_impl!(
    op, v::AbstractArray, backend::Backend;
    init,
    neutral=GPUArrays.neutral_element(op, eltype(v)),
    dims::Union{Nothing, Int}=nothing,
    inclusive::Bool=true,

    alg::AccumulateAlgorithm=DecoupledLookback(),

    # GPU settings
    block_size::Int=256,
    temp::Union{Nothing, AbstractArray}=nothing,
    temp_flags::Union{Nothing, AbstractArray}=nothing,
)
    if backend isa GPU
        if isnothing(dims)
            return accumulate_1d!(
                op, v, backend, alg,
                init=init, neutral=neutral, inclusive=inclusive,
                block_size=block_size, temp=temp, temp_flags=temp_flags,
            )
        else
            return accumulate_nd!(
                op, v, backend,
                init=init, neutral=neutral, dims=dims, inclusive=inclusive,
                block_size=block_size,
            )
        end
    else
        if isnothing(dims)
            return accumulate_1d!(
                op, v,
                init=init, inclusive=inclusive,
            )
        else
            return accumulate_nd!(
                op, v,
                init=init, dims=dims, inclusive=inclusive,
            )
        end
    end
end


"""
    accumulate(
        op, v::AbstractArray, backend::Backend=get_backend(v);
        init,
        neutral=GPUArrays.neutral_element(op, eltype(v)),
        dims::Union{Nothing, Int}=nothing,
        inclusive::Bool=true,

        # Algorithm choice
        alg::AccumulateAlgorithm=DecoupledLookback(),

        # GPU settings
        block_size::Int=256,
        temp::Union{Nothing, AbstractArray}=nothing,
        temp_flags::Union{Nothing, AbstractArray}=nothing,
    )

Out-of-place version of [`accumulate!`](@ref).
"""
function accumulate(
    op, v::AbstractArray, backend::Backend=get_backend(v);
    init,
    neutral=GPUArrays.neutral_element(op, eltype(v)),
    dims::Union{Nothing, Int}=nothing,
    inclusive::Bool=true,

    # Algorithm choice
    alg::AccumulateAlgorithm=DecoupledLookback(),

    # GPU settings
    block_size::Int=256,
    temp::Union{Nothing, AbstractArray}=nothing,
    temp_flags::Union{Nothing, AbstractArray}=nothing,
)
    dst_type = promote_type(eltype(v), typeof(init))
    vcopy = similar(v, dst_type)
    copyto!(vcopy, v)
    accumulate!(
        op, vcopy, backend;
        init=init,
        neutral=neutral,
        dims=dims,
        inclusive=inclusive,

        alg=alg,
        
        block_size=block_size,
        temp=temp,
        temp_flags=temp_flags,
    )
    vcopy
end

